import "pkg:/components/sdk/helpers/RequestHelper.bs"
import "pkg:/source/constants/Events.bs"
import "pkg:/source/facades/RouterFacade.bs"
import "pkg:/source/RouteDefintions.bs"
import "pkg:/source/sdk/core/EventBus.bs"
import "pkg:/source/Theme.bs"

sub init()
    #if insert_tracker_task
        m.trackerTask = m.top.createChild("TrackerTask")
    #end if

    m._dataLoaded = false
    m._exitModal = invalid
    m._initialData = invalid
    m._splashTimerFired = false

    m.router = m.top.findNode("appRouter")
    m.minimumSplashTimer = m.top.findNode("minimumSplashTimer")

    m.minimumSplashTimer.observeFieldScoped("fire", "onMinimumSplashTimerFired")

    SDK.Core.EventBus.on(App.Constants.Events.EXIT_APP, onExitApp)
    SDK.Core.EventBus.on(App.Constants.Events.CLOSE_EXIT_CONFIRMATION, onCloseExitConfirmation)

    _setGlobals()
    _navigateToSplashScreen()
    _loadData()
end sub

'-----------------
' EVENT HANDLERS
' -----------------

sub onMinimumSplashTimerFired()
    m._splashTimerFired = true
    if m._dataLoaded then _navigateToHomeScreen(m._initialData)
end sub

sub onExitApp(payload = invalid as Object)
    m.top.exitApp = true
end sub

sub onCloseExitConfirmation(payload = invalid as Object)
    _hideExitConfirmation()
end sub

'-----------------
' PRIVATE METHODS
' -----------------

sub _setGlobals()
    App.Navigation.Router.setGlobalRouter(m.global, m.router)

    m.global.addFields({
        theme: {
            colors: App.Theme.COLORS
            fonts: App.Theme.FONTS
        }
    })
end sub

sub _navigateToSplashScreen()
    App.Navigation.Router.NavigateTo(App.Constants.RouteDefintions.SPLASH)
    _startMinimumSplashTimer()
end sub

sub _navigateToHomeScreen(data as Object)
    App.Navigation.Router.NavigateTo(App.Constants.RouteDefintions.HOME, { data: data })
end sub

sub _startMinimumSplashTimer()
    m.minimumSplashTimer.duration = App.Theme.MINIMUM_SPLASH_DURATION
    m.minimumSplashTimer.control = "start"
end sub

sub _loadData()
    App.Helpers.HTTP.makeRequest("GET", "https://api.tvmaze.com/shows", invalid, sub(response as Object)
        if NOT response.ok
            App.Navigation.Router.NavigateTo(App.Constants.RouteDefintions.ERROR)
            return
        end if

        m._initialData = response.json
        m._dataLoaded = true
        if m._splashTimerFired then _navigateToHomeScreen(m._initialData)
    end sub)
end sub

sub _showExitConfirmation()
    if m._exitModal = invalid
        m._exitModal = m.top.createChild("ExitConfirmationModal")
    end if

    m._exitModal@.setFocus(true)
end sub

sub _hideExitConfirmation()
    if m._exitModal = invalid then return

    m.top.removeChild(m._exitModal)
    m._exitModal = invalid

    ' Restore focus to the current screen
    App.Navigation.Router.setFocus(true)
end sub

'-----------------
' KEY HANDLING
' -----------------

function onKeyEvent(key as String, press as Boolean) as Boolean
    if press = false return false

    if key = "back"
        didGoBack = App.Navigation.Router.goBack()
        if NOT didGoBack then _showExitConfirmation()
    
        return true
    end if

    return false
end function
