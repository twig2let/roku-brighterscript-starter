' Router Fa√ßade
' - Lightweight accessors for the global Router node.
' - Must be initialized via setGlobalRouter during app startup.
'
' Example usage:
'   SDK.Navigation.Router.navigateTo(App.Constants.RouteDefintions.HOME)
'   SDK.Navigation.Router.navigateTo(App.Constants.RouteDefintions.DETAILS, {
'       data: { id: "abc123" }
'   })
'   SDK.Navigation.Router.goBack({ jumpToRowItem: 3 })
'   SDK.Navigation.Router.setFocus(true)

namespace SDK.Navigation.Router

    ' -------------------
    ' PUBLIC METHODS
    ' -------------------

    sub setGlobalRouter(globalNode as Object, router as Object)
        if type(globalNode) <> "roSGNode" then return
        globalNode.addFields({ router: router })
    end sub

    ' routeState will be passed back to the route on a back journey
    ' - Params are delivered to the target screen as `params`.
    ' - `routeState` is returned later as `params.backState` on back journeys.
    sub navigateTo(route as Object, params = {} as Object, routeState = invalid as Object)
        router = _getRouter()
        if router = invalid
            print "[RouterFacade] Router not initialised"
            return
        end if

        router@.navigateTo(route, params, routeState)
    end sub

    function goBack(params = invalid as Object) as Boolean
        router = _getRouter()
        if router = invalid
            print "[RouterFacade] Router not initialised"
            return false
        end if

        return router@.goBack(params)
    end function

    function setFocus(focused as Boolean) as Boolean
        router = _getRouter()
        if router = invalid
            print "[RouterFacade] Router not initialised"
            return false
        end if

        return router@.setFocus(focused)
    end function

    ' -------------------
    ' PRIVATE METHODS
    ' -------------------

    function _getRouter() as Object
        return m.global.router
    end function

end namespace
